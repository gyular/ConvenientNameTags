import com.modrinth.minotaur.dependencies.ModDependency

plugins {
  id "fabric-loom" version "0.12-SNAPSHOT"
  id "maven-publish"
  id "com.modrinth.minotaur" version "2.+"
  id "com.matthewprenger.cursegradle" version "1.4.0"
  id "co.uzzu.dotenv.gradle" version "2.0.0"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
group = project.maven_group
version = project.mod_version
def modVersionType = project.mod_version_type
def mcVersion = project.minecraft_version


if (env.MODRINTH_TOKEN.isPresent()) {
  println("Modrinth token present")
  modrinth {
    token = env.MODRINTH_TOKEN.value
    projectId = "eUsHC1iS"
    uploadFile = remapJar
    versionType = modVersionType
    versionName = "convenientnametags-${mcVersion}-${modVersionType}-${version}"
    changelog = rootProject.file("changelogs/convenientnametags_${version}.md").text
    syncBodyFrom = rootProject.file("README.md").text
    gameVersions = ["1.19, 1.19.1, 1.19.2"]
    loaders = ["fabric"]
    dependencies {
      required.project "fabric-api"
    }
  }
}

if (env.CURSEGRADLE_TOKEN.isPresent()) {
  println("Cursegradle token present")
  curseforge {
    apiKey = env.CURSEGRADLE_TOKEN.value
    project {
      id = "662763"
      changelog = rootProject.file("changelogs/convenientnametags_${version}.md").text
      changelogType = "markdown"
      releaseType = modVersionType
      addGameVersion "Fabric"
      addGameVersion "1.19"
      addGameVersion "1.19.1"
      addGameVersion "1.19.2"
      relations {
        requiredLibrary "fabric-api"
      }
      afterEvaluate {
        mainArtifact(remapJar) {
          displayName = "convenientnametags-${mcVersion}-${modVersionType}-${version}"
        }
        uploadTask.dependsOn(remapJar)
      }
    }
    options {
      forgeGradleIntegration = false
    }
  }
}

repositories {
  mavenCentral()
  maven { url 'https://jitpack.io' }
}

dependencies {
  minecraft "com.mojang:minecraft:${mcVersion}"
  mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
  modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
  modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
}

processResources {
  inputs.property "version", project.version

  filesMatching("fabric.mod.json") {
    expand "version": project.version
  }
}

tasks.withType(JavaCompile).configureEach {
  it.options.release.set(17)
}

java {
  withSourcesJar()
}

jar {
  from("LICENSE") {
    rename { "${it}_${archivesBaseName}" }
  }
}